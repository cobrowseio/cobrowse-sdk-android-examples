// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.2'
        classpath 'com.google.gms:google-services:4.3.15'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.7.0"
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.10'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    afterEvaluate { Project project ->
        File propsFile
        if (project.name == 'standalone') {
            propsFile = project.file('standalone.jks.properties')
        } else if (project.name == 'sample') {
            propsFile = project.file('cobrowse.keystore.properties')
        } else {
            return
        }

        if (!project.hasProperty("android")) {
            throw new GradleException("${project.name} project is not Android?")
        }

        if (!propsFile.exists()) {
            project.logger.warn("Signing configuration at ${propsFile.name} is not found")
            return
        }

        def props = new Properties()
        props.load(new FileInputStream(propsFile))

        android {
            signingConfigs {
                release {
                    keyAlias props['keyAlias']
                    keyPassword props['keyPassword']
                    storeFile project.file(props['storeFile'])
                    storePassword props['storePassword']
                }
            }
            buildTypes {
                release {
                    signingConfig signingConfigs.release
                }
                debug {
                    signingConfig signingConfigs.release
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}